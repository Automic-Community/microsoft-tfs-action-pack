*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        CHECKIN
Default Tags      win
Test Template     Checkin Template
Resource          ../../resources/keywords.txt
Resource          prt_keywords.txt

*** Variables ***
${CHECKIN_working_dir}    ${_WORKING_DIR}${/}CHECKIN    # Working dir for testing CHECKIN action
${CHECKIN_workspace}    ${PREFIX}_CHECKIN_WS    #workspace for testing CHECKIN action
${CHECKIN_branch}    ${PREFIX}_CHECKIN_TEST    #branch for testing CHECKIN action

*** Test Cases ***    OK_NOK     TF Utility Path            Working Dir                 Author           File                                      Comment         Override    Reason     Recursive    RETURN CODE    RETURN STATUS    MESSAGE
01_Success Checkin    [Setup]    Add File Before Checkin    ${PREFIX}_CHECKIN_01.txt
                      OK         ${_TFS_UTIL_PATH}          ${CHECKIN_working_dir}      Administrator    ${_TFS_BASE_ITEM}${/}${CHECKIN_branch}    Test checkin    No          AT Test    Yes          0              ${ENDED_OK}      ${EMPTY}

02_Fail Wrong Utility Path
                      NOK        wrong                      ${CHECKIN_working_dir}      Administrator    ${_TFS_BASE_ITEM}${/}${CHECKIN_branch}    Test checkin    No          AT Test    Yes          0              ${ENDED_NOK}     ERROR[TF Utility Path]

03_Fail Wrong Working Dir Path
                      NOK        ${_TFS_UTIL_PATH}          C:\\Projectswrong           Administrator    ${_TFS_BASE_ITEM}${/}${CHECKIN_branch}    Test checkin    No          AT Test    Yes          0              ${ENDED_NOK}     ERROR[Working Directory]

04_Fail Wrong Author
                      NOK        ${_TFS_UTIL_PATH}          ${CHECKIN_working_dir}      wrong            ${_TFS_BASE_ITEM}${/}${CHECKIN_branch}    Test checkin    No          AT Test    Yes          0              ${ENDED_NOK}     ${EMPTY}

05_Fail Checkin Recursive No
                      NOK        ${_TFS_UTIL_PATH}          ${CHECKIN_working_dir}      Administrator    ${_TFS_BASE_ITEM}${/}${CHECKIN_branch}    Test checkin    No          AT Test    No           0              ${ENDED_NOK}     ${EMPTY}

*** Keywords ***
Checkin Template
    [Arguments]    ${OK_NOK}    ${TFS_UTIL_PATH}    ${TFS_WORKING_DIR}    ${TFS_AUTHOR}    ${TFS_FILE}    ${TFS_COMMENT}
    ...    ${TFS_OVERRIDE}    ${TFS_REASON}    ${TFS_RECURSIVE}    ${returnCode}    ${returnStatus}    ${message}
    [Documentation]    This template is used for test CHECKIN with needed arguments
    ${_CURRENT_TIMESTAMP_}    Get Time    epochs
    Init Action    ${ACTION_CHECKIN}
    Action Set    ${PRT_CHECKIN_TFS_UTIL_PATH}    ${TFS_UTIL_PATH}
    Action Set    ${PRT_CHECKIN_TFS_WORKING_DIR}    ${TFS_WORKING_DIR}
    Action Set    ${PRT_CHECKIN_TFS_AUTHOR}    ${TFS_AUTHOR}
    Action Set    ${PRT_CHECKIN_TFS_FILE}    ${TFS_FILE}
    Action Set    ${PRT_CHECKIN_TFS_COMMENT}    ${TFS_COMMENT}_${_CURRENT_TIMESTAMP_}
    Action Set    ${PRT_CHECKIN_TFS_OVERRIDE}    ${TFS_OVERRIDE}
    Action Set    ${PRT_CHECKIN_TFS_REASON}    ${TFS_REASON}
    Action Set    ${PRT_CHECKIN_TFS_RECURSIVE}    ${TFS_RECURSIVE}
    Action Set Timeout    300
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}'=='OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Action Report Should Contain    ${message}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Report Should Contain    ${footer_logging_NOK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Start testing for Checkin Template
    Log    Delete Branch from last test
    Delete Branch    ${CHECKIN_working_dir}    ${PREFIX}_CHECKIN_*
    Log    Delete Workspace from last test
    Delete Workspace    ${CHECKIN_workspace}
    Log    Delete Working Dir from previous test
    Delete Dir    ${CHECKIN_working_dir}
    Log    Finish remove data from previous test. Preparing test data...
    Create Dir    ${CHECKIN_working_dir}
    Create Workspace    ${CHECKIN_working_dir}    ${CHECKIN_workspace}
    Get Source    ${CHECKIN_working_dir}    ${_TFS_BASE_ITEM}    ${_TFS_BASE_VERSION}
    Create Branch    ${CHECKIN_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    ${_TFS_BASE_ITEM}\\${CHECKIN_branch}

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing for Checkin Template end

Add File Before Checkin
    [Arguments]    ${newFile}
    Delete Item from TFS    ${CHECKIN_working_dir}    ${_TFS_BASE_ITEM}${/}${CHECKIN_branch}${/}${newFile}
    Create File    ${CHECKIN_working_dir}${/}${_TFS_BASE_ITEM}${/}${CHECKIN_branch}${/}${newFile}
    Add Item To TFS    ${CHECKIN_working_dir}    ${CHECKIN_working_dir}${/}${_TFS_BASE_ITEM}${/}${CHECKIN_branch}${/}${newFile}
