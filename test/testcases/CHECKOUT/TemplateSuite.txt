*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        CHECKOUT
Default Tags      win
Test Template     Checkout Template
Resource          ../../resources/keywords.txt
Resource          prt_keywords.txt

*** Variables ***
${CHECKOUT_working_dir}    ${_WORKING_DIR}${/}CHECKOUT    # Working dir for testing CHECKOUT action
${CHECKOUT_workspace}    ${PREFIX}_CHECKOUT_WS    #workspace for testing CHECKOUT action
${CHECKOUT_branch}    ${PREFIX}_CHECKOUT_TEST    #branch for testing CHECKOUT action

*** Test Cases ***    OK_NOK    TF Utility Path      Working Dir                Item                                     Recursive    Lock    RETURN CODE    RETURN STATUS    MESSAGE
01_Success Checkout
                      OK        ${_TFS_UTIL_PATH}    ${CHECKOUT_working_dir}    ${_TFS_BASE_ITEM}\\${CHECKOUT_branch}    Yes          NONE    0              ${ENDED_OK}      Projects\FIBO_CHECKOUT_TEST

02_Fail Checkout Wrong utility Path
                      NOK       wrong                ${CHECKOUT_working_dir}    ${_TFS_BASE_ITEM}\\${CHECKOUT_branch}    Yes          NONE    0              ${ENDED_NOK}     ERROR[TF Utility Path]

03_Fail Checkout Wrong Working Dir Path
                      NOK       ${_TFS_UTIL_PATH}    wrong                      ${_TFS_BASE_ITEM}\\${CHECKOUT_branch}    Yes          NONE    0              ${ENDED_NOK}     ERROR[Working Directory]

04_Fail Checkout Not Exist item
                      NOK       ${_TFS_UTIL_PATH}    ${CHECKOUT_working_dir}    wrong                                    Yes          NONE    0              ${ENDED_NOK}     No matching items found in

05_Fail Checkout Recursive No
                      NOK       ${_TFS_UTIL_PATH}    ${CHECKOUT_working_dir}    ${_TFS_BASE_ITEM}\\${CHECKOUT_branch}    No           NONE    0              ${ENDED_NOK}     TF14040: The folder $/Projects/FIBO_CHECKOUT_TEST may not be checked out.

*** Keywords ***
Checkout Template
    [Arguments]    ${OK_NOK}    ${TFS_UTIL_PATH}    ${TFS_WORKING_DIR}    ${TFS_ITEM}    ${TFS_RECURSIVE}    ${TFS_LOCK}
    ...    ${returnCode}    ${returnStatus}    ${message}
    [Documentation]    This template is used for test CHECKOUT with needed arguments
    Init Action    ${ACTION_CHECKOUT}
    Action Set    ${PRT_CHECKOUT_TFS_UTIL_PATH}    ${TFS_UTIL_PATH}
    Action Set    ${PRT_CHECKOUT_TFS_WORKING_DIR}    ${TFS_WORKING_DIR}
    Action Set    ${PRT_CHECKOUT_TFS_ITEM}    ${TFS_ITEM}
    Action Set    ${PRT_CHECKOUT_TFS_RECURSIVE}    ${TFS_RECURSIVE}
    Action Set    ${PRT_CHECKOUT_TFS_LOCK}    ${TFS_LOCK}
    Action Set Timeout    300
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}'=='OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Action Report Should Contain    ${message}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Report Should Contain    ${footer_logging_NOK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Start testing for Checkout Template
    Log    Delete Working Dir from previous test
    Delete Dir    ${CHECKOUT_working_dir}
    Log    Delete Branch from last test
    Delete Branch    ${CHECKOUT_working_dir}    ${PREFIX}_CHECKOUT_*
    Log    Delete Workspace from last test
    Delete Workspace    ${CHECKOUT_workspace}
    Log    Finish remove data from previous test. Preparing test data...
    Create Dir    ${CHECKOUT_working_dir}
    Create Workspace    ${CHECKOUT_working_dir}    ${CHECKOUT_workspace}
    Get Source    ${CHECKOUT_working_dir}    ${_TFS_BASE_ITEM}    ${_TFS_BASE_VERSION}
    Create Branch    ${CHECKOUT_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    ${_TFS_BASE_ITEM}\\${CHECKOUT_branch}

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing for Checkout Template end
