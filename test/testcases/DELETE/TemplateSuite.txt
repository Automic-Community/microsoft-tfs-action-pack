*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        DELETE
Default Tags      win
Test Template     Delete Template
Resource          ../../resources/keywords.txt
Resource          prt_keywords.txt

*** Variables ***
${DELETE_working_dir}    ${_WORKING_DIR}${/}DELETE    # Working dir for testing DELETE action
${DELETE_workspace}    ${PREFIX}_DELETE_WS    #workspace for testing DELETE action

*** Test Cases ***    OK_NOK    TF Utility Path      Working Directory        Item                               Recursive    Lock    RETURN CODE    RETURN STATUS    MESSAGE
01_Success Delete     OK        ${_TFS_UTIL_PATH}    ${DELETE_working_dir}    ${_TFS_BASE_ITEM}\\test.txt        Yes          NONE    0              ${ENDED_OK}      Projects:

02_Fail Delete Wrong utility path
                      NOK       wrong                ${DELETE_working_dir}    ${_TFS_BASE_ITEM}\\new_file.txt    Yes          NONE    0              ${ENDED_NOK}     ERROR[TF Utility Path]

03_Fail Delete Wrong working dir path
                      NOK       ${_TFS_UTIL_PATH}    wrong                    ${_TFS_BASE_ITEM}\\new_file.txt    Yes          NONE    0              ${ENDED_NOK}     ERROR[Working Directory]

04_Fail Delete Wrong item
                      NOK       ${_TFS_UTIL_PATH}    ${DELETE_working_dir}    wrong                              Yes          NONE    0              ${ENDED_NOK}     No matching items found in

*** Keywords ***
Delete Template
    [Arguments]    ${OK_NOK}    ${TFS_UTIL_PATH}    ${TFS_WORKING_DIR}    ${TFS_ITEM}    ${TFS_RECURSIVE}    ${TFS_LOCK}
    ...    ${returnCode}    ${returnStatus}    ${message}
    [Documentation]    This template is used for test DELETE with needed arguments
    Init Action    ${ACTION_DELETE}
    Action Set    ${PRT_DELETE_TFS_UTIL_PATH}    ${TFS_UTIL_PATH}
    Action Set    ${PRT_DELETE_TFS_WORKING_DIR}    ${TFS_WORKING_DIR}
    Action Set    ${PRT_DELETE_TFS_ITEM}    ${TFS_ITEM}
    Action Set    ${PRT_DELETE_TFS_RECURSIVE}    ${TFS_RECURSIVE}
    Action Set    ${PRT_DELETE_TFS_LOCK}    ${TFS_LOCK}
    Action Set Timeout    300
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}'=='OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Action Report Should Contain    ${message}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Report Should Contain    ${footer_logging_NOK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Start testing for Delete Template
    Log    Delete Working Dir from previous test
    Delete Dir    ${DELETE_working_dir}
    Log    Delete Workspace from last test
    Delete Workspace    ${DELETE_workspace}
    Log    Finish remove data from previous test. Preparing test data...
    Create Dir    ${DELETE_working_dir}
    Create Workspace    ${DELETE_working_dir}    ${DELETE_workspace}
    Get Source    ${DELETE_working_dir}    ${_TFS_BASE_ITEM}    ${_TFS_BASE_VERSION}

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing for Delete Template end
