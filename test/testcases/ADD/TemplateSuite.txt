*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        ADD
Default Tags      win
Test Template     Add Template
Resource          ../../resources/keywords.txt
Resource          prt_keywords.txt

*** Variables ***
${ADD_working_dir}    ${_WORKING_DIR}${/}ADD    # Working dir for testing ADD action
${ADD_workspace}    ${PREFIX}_ADD_WS    #workspace for testing ADD action
${ADD_branch}     ${PREFIX}_ADD_TEST    #branch for testing ADD action

*** Test Cases ***    OK_NOK     TF Utility Path      Working Dir                                                                         Path                                                        Recursive    Lock    RETURN CODE    RETURN STATUS    MESSAGE
01_Success Add        [Setup]    Create File          ${ADD_working_dir}${/}${_TFS_BASE_ITEM}${/}${ADD_branch}${/}${PREFIX}_ADD_01.txt
                      OK         ${_TFS_UTIL_PATH}    ${ADD_working_dir}                                                                  ${ADD_working_dir}${/}${_TFS_BASE_ITEM}${/}${ADD_branch}    Yes          NONE    0              ${ENDED_OK}      Start adding files & folders to TFS

02_Fail Add Using wrong utility path
                      NOK        wrong                ${ADD_working_dir}                                                                  ${ADD_working_dir}${/}${_TFS_BASE_ITEM}${/}${ADD_branch}    Yes          NONE    0              ${ENDED_NOK}     ERROR[TF Utility Path]

03_Fail Add Using wrong Working Dir path
                      NOK        ${_TFS_UTIL_PATH}    wrong                                                                               ${ADD_working_dir}${/}${_TFS_BASE_ITEM}${/}${ADD_branch}    Yes          NONE    0              ${ENDED_NOK}     ERROR[Working Directory]

04_Fail Add Using wrong add path
                      NOK        ${_TFS_UTIL_PATH}    ${ADD_working_dir}                                                                  wrong\\xml                                                  Yes          NONE    0              ${ENDED_NOK}     Could not find a part of the path

*** Keywords ***
Add Template
    [Arguments]    ${OK_NOK}    ${TFS_UTIL_PATH}    ${TFS_WORKING_DIR}    ${TFS_ADD_PATH}    ${TFS_RECURSIVE}    ${TFS_LOCK}
    ...    ${returnCode}    ${returnStatus}    ${message}
    [Documentation]    This template is used for test ADD with needed arguments
    ${_CURRENT_TIMESTAMP_}    Get Time    epochs
    Init Action    ${ACTION_ADD}
    Action Set    ${PRT_ADD_TFS_UTIL_PATH}    ${TFS_UTIL_PATH}
    Action Set    ${PRT_ADD_TFS_WORKING_DIR}    ${TFS_WORKING_DIR}
    Action Set    ${PRT_ADD_TFS_ADD_PATH}    ${TFS_ADD_PATH}
    Action Set    ${PRT_ADD_TFS_RECURSIVE}    ${TFS_RECURSIVE}
    Action Set    ${PRT_ADD_TFS_LOCK}    ${TFS_LOCK}
    Action Set Timeout    300
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}'=='OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Action Report Should Contain    ${message}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Report Should Contain    ${footer_logging_NOK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Start testing for Add Template
    Log    Delete Working Dir from previous test
    Delete Dir    ${ADD_working_dir}
    Log    Delete Workspace from last test
    Delete Workspace    ${ADD_workspace}
    Log    Delete Branch from last test
    Delete Branch    ${ADD_working_dir}    ${ADD_branch}
    Log    Finish remove data from previous test. Preparing test data...
    Create Dir    ${ADD_working_dir}
    Create Workspace    ${ADD_working_dir}    ${ADD_workspace}
    Get Source    ${ADD_working_dir}    ${_TFS_BASE_ITEM}    ${_TFS_BASE_VERSION}
    Create Branch    ${ADD_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    ${_TFS_BASE_ITEM}\\${ADD_branch}

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing for Add Template end
