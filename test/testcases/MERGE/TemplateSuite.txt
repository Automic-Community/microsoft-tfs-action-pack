*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        MERGE
Default Tags      win
Test Template     Merge Template
Resource          ../../resources/keywords.txt
Resource          prt_keywords.txt

*** Variables ***
${MERGE_working_dir}    ${_WORKING_DIR}${/}MERGE    # Working dir for testing MERGE action
${MERGE_workspace}    ${PREFIX}_MERGE_WS    #workspace for testing MERGE action
${MERGE_branch}    ${PREFIX}_MERGE_TEST    #branch for testing MERGE action
${MERGE_branch_target}    MyProject-branch

*** Test Cases ***    OK_NOK     TF Utility Path                       Working Dir                   Version                  Source                      Destination                         Recursive    Lock    Force    Discard    Baseless    Candidate    RETURN CODE    RETURN STATUS    MESSAGE
01_Success Merge 2 branches in same versions
                      [Setup]    Mapping Working Dir with Workspace    ${MERGE_working_dir}${/}01    ${MERGE_workspace}_01
                      OK         ${_TFS_UTIL_PATH}                     ${MERGE_working_dir}${/}01    ${EMPTY}                 Projects\\MyProject         Projects\\${MERGE_branch_target}    Yes          NONE    No       No         No          No           0              ${ENDED_OK}      There are no changes to merge

02_Fail Merge Wrong utility path
                      [Setup]    Mapping Working Dir with Workspace    ${MERGE_working_dir}${/}02    ${MERGE_workspace}_02
                      NOK        wrong                                 ${MERGE_working_dir}${/}02    ${_TFS_BASE_VERSION}     Projects\\MyProject\\xml    Projects\\${MERGE_branch_target}    Yes          NONE    No       No         No          No           0              ${ENDED_NOK}     ERROR[TF Utility Path]

03_Fail Merge Wrong Working Dir path
                      [Setup]    Mapping Working Dir with Workspace    ${MERGE_working_dir}${/}03    ${MERGE_workspace}_03
                      NOK        ${_TFS_UTIL_PATH}                     C:\\Projectswrong             ${_TFS_BASE_VERSION}     Projects\\MyProject\\xml    Projects\\${MERGE_branch_target}    Yes          NONE    No       No         No          No           0              ${ENDED_NOK}     ERROR[Working Directory]

04_Fail Merge Wrong item
                      [Setup]    Mapping Working Dir with Workspace    ${MERGE_working_dir}${/}04    ${MERGE_workspace}_04
                      NOK        ${_TFS_UTIL_PATH}                     ${MERGE_working_dir}${/}04    ${_TFS_BASE_VERSION}     wrong                       Projects\\${MERGE_branch_target}    Yes          NONE    No       No         No          No           0              ${ENDED_NOK}     No matching items found in

05_Fail Merge Wrong version
                      [Setup]    Mapping Working Dir with Workspace    ${MERGE_working_dir}${/}05    ${MERGE_workspace}_05
                      NOK        ${_TFS_UTIL_PATH}                     ${MERGE_working_dir}${/}05    No                       Projects\\MyProject\\xml    Projects\\${MERGE_branch_target}    Yes          NONE    No       No         No          No           0              ${ENDED_NOK}     'N' is not a supported version specification type

06_Success Merge 2 branches in different versions
                      [Setup]    Mapping Working Dir with Workspace    ${MERGE_working_dir}${/}06    ${MERGE_workspace}_06
                      OK         ${_TFS_UTIL_PATH}                     ${MERGE_working_dir}${/}06    T                        Projects\\MyProject         Projects\\${MERGE_branch_target}    Yes          NONE    Yes      No         No          No           0              ${ENDED_OK}      merge: $

07_Fail to Merge due to not input version
                      [Setup]    Mapping Working Dir with Workspace    ${MERGE_working_dir}${/}07    ${MERGE_workspace}_07
                      NOK        ${_TFS_UTIL_PATH}                     ${MERGE_working_dir}${/}07    ${EMPTY}                 Projects\\MyProject         Projects\\${MERGE_branch_target}    Yes          NONE    Yes      No         No          No           0              ${ENDED_NOK}     A version or version range must be specified when using the /force option

*** Keywords ***
Merge Template
    [Arguments]    ${OK_NOK}    ${TFS_UTIL_PATH}    ${TFS_WORKING_DIR}    ${TFS_VERSION}    ${TFS_SOURCE}    ${TFS_DESTINATION}
    ...    ${TFS_RECURSIVE}    ${TFS_LOCK}    ${TFS_FORCE}    ${TFS_DISCARD}    ${TFS_BASELESS}    ${TFS_CANDIDATE}
    ...    ${returnCode}    ${returnStatus}    ${message}
    [Documentation]    This template is used for test MERGE with needed arguments
    Init Action    ${ACTION_MERGE}
    Action Set    ${PRT_MERGE_TFS_UTIL_PATH}    ${TFS_UTIL_PATH}
    Action Set    ${PRT_MERGE_TFS_WORKING_DIR}    ${TFS_WORKING_DIR}
    Action Set    ${PRT_MERGE_TFS_VERSION}    ${TFS_VERSION}
    Action Set    ${PRT_MERGE_TFS_SOURCE}    ${TFS_SOURCE}
    Action Set    ${PRT_MERGE_TFS_DESTINATION}    ${TFS_DESTINATION}
    Action Set    ${PRT_MERGE_TFS_RECURSIVE}    ${TFS_RECURSIVE}
    Action Set    ${PRT_MERGE_TFS_LOCK}    ${TFS_LOCK}
    Action Set    ${PRT_MERGE_TFS_FORCE}    ${TFS_FORCE}
    Action Set    ${PRT_MERGE_TFS_DISCARD}    ${TFS_DISCARD}
    Action Set    ${PRT_MERGE_TFS_BASELESS}    ${TFS_BASELESS}
    Action Set    ${PRT_MERGE_TFS_CANDIDATE}    ${TFS_CANDIDATE}
    Action Set Timeout    300
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}'=='OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Action Report Should Contain    ${message}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Report Should Contain    ${footer_logging_NOK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Start testing for Merge Template

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing for Merge Template end

Mapping Working Dir with Workspace
    [Arguments]    ${WorkingDir}    ${Workspace}
    Log    Delete Workspace from last test
    Delete Workspace    ${Workspace}
    Log    Delete Working Dir from previous test
    Delete Dir    ${WorkingDir}
    Log    Finish remove data from previous test. Preparing test data...
    Create Dir    ${WorkingDir}
    Create Workspace    ${WorkingDir}    ${Workspace}
    Get Source    ${WorkingDir}    ${_TFS_BASE_ITEM}    ${_TFS_BASE_VERSION}
