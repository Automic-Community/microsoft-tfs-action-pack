*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        CREATE WORKSPACE
Default Tags      win
Test Template     Create Workspace Template
Resource          ../../resources/keywords.txt
Resource          prt_keywords.txt

*** Variables ***
${CREATE_WORKSPACE_working_dir}    ${_WORKING_DIR}${/}CREATE_WORKSPACE    # Working dir for testing CREATE_WORKSPACE action
${CREATE_WORKSPACE_workspace}    ${PREFIX}_CREATE_WORKSPACE_WS    #workspace for testing CREATE_WORKSPACE action
${CREATE_WORKSPACE_branch}    ${PREFIX}_CREATE_WORKSPACE_TEST    #branch for testing CREATE_WORKSPACE action
${TFS_WS_OWNER}    VVNSATTFS01\\Administrator

*** Test Cases ***    OK_NOK     TF Utility Path           Working Directory                           Workspace                             Owner              Server                  Comment                              RETURN CODE    RETURN STATUS    MESSAGE
01_Success Create Workspace
                      [Setup]    Create New Working Dir    ${CREATE_WORKSPACE_working_dir}${/}01       ${CREATE_WORKSPACE_workspace}_01
                      OK         ${_TFS_UTIL_PATH}         ${CREATE_WORKSPACE_working_dir}${/}01       ${CREATE_WORKSPACE_workspace}_01      ${TFS_WS_OWNER}    ${_TFS_SERVER}          01_Success Create Workspace          0              ${ENDED_OK}      INFO: Start create workspace

02_Fail Create Workspace Using wrong utility path
                      [Setup]    Create New Working Dir    ${CREATE_WORKSPACE_working_dir}${/}Fail     ${CREATE_WORKSPACE_workspace}_Fail
                      NOK        wrong                     ${CREATE_WORKSPACE_working_dir}${/}Fail     ${CREATE_WORKSPACE_workspace}_Fail    ${TFS_WS_OWNER}    ${_TFS_SERVER}          02_Fail Create Wrong utility path    0              ${ENDED_NOK}     ERROR[TF Utility Path]

03_Fail Create Workspace Using wrong working dir
                      NOK        ${_TFS_UTIL_PATH}         ${CREATE_WORKSPACE_working_dir}_20160101    ${CREATE_WORKSPACE_workspace}_Fail    ${TFS_WS_OWNER}    ${_TFS_SERVER}          03_Fail Create Wrong working dir     0              ${ENDED_NOK}     ERROR[Working Directory]

04_Fail Create Workspace Using wrong owner
                      NOK        ${_TFS_UTIL_PATH}         ${CREATE_WORKSPACE_working_dir}${/}Fail     ${CREATE_WORKSPACE_workspace}_Fail    inferno            ${_TFS_SERVER}          04_Fail Create Wrong owner           0              ${ENDED_NOK}     TF14045:

05_Fail Create Workspace Using wrong server
                      NOK        ${_TFS_UTIL_PATH}         ${CREATE_WORKSPACE_working_dir}${/}Fail     ${CREATE_WORKSPACE_workspace}_Fail    ${TFS_WS_OWNER}    http://vvntfs01:8080    05_Fail Create Wrong server          0              ${ENDED_NOK}     ${EMPTY}                        # the message are different from tfs version, TF400324: Team Foundation services are not available from server

*** Keywords ***
Create Workspace Template
    [Arguments]    ${OK_NOK}    ${TFS_UTIL_PATH}    ${TFS_WORKING_DIR}    ${TFS_NAME}    ${TFS_OWNER}    ${TFS_SERVER}
    ...    ${TFS_COMMENT}    ${returnCode}    ${returnStatus}    ${message}
    [Documentation]    This template is used for test Create Workspace with needed arguments
    Init Action    ${ACTION_CREATEWS}
    Action Set    ${PRT_CREATEWS_TFS_UTIL_PATH}    ${TFS_UTIL_PATH}
    Action Set    ${PRT_CREATEWS_TFS_WORKING_DIR}    ${TFS_WORKING_DIR}
    Action Set    ${PRT_CREATEWS_TFS_NAME}    ${TFS_NAME}
    Action Set    ${PRT_CREATEWS_TFS_OWNER}    ${TFS_OWNER}
    Action Set    ${PRT_CREATEWS_TFS_SERVER}    ${TFS_SERVER}
    Action Set    ${PRT_CREATEWS_TFS_COMMENT}    ${TFS_COMMENT}
    Action Set Timeout    300
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}'=='OK'    Action Return Code Should Be    ${returnCode}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Return Code Should Not Equal    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Action Report Should Contain    ${message}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}
    Run Keyword If    '${OK_NOK}'=='NOK'    Action Report Should Contain    ${footer_logging_NOK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Start testing for Create Workspace Template

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing for Create Workspace Template end

Create New Working Dir
    [Arguments]    ${workingDir}    ${workspaceName}
    Log    Delete Working Dir from previous test
    Delete Dir    ${workingDir}
    Log    Delete Workspace from last test
    Delete Workspace    ${workspaceName}
    Log    Finish remove data from previous test. Preparing test data...
    Create Dir    ${workingDir}
