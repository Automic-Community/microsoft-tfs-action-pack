*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        GET
Default Tags      win
Test Template     Get Template
Resource          ../../resources/keywords.txt
Resource          prt_keywords.txt

*** Variables ***
${GET_working_dir}    ${_WORKING_DIR}${/}GET    # Working dir for testing GET action
${GET_workspace}    ${PREFIX}_GET_WS    #workspace for testing GET action
${GET_branch}     ${PREFIX}_GET_TEST    #branch for testing GET action

*** Test Cases ***    OK_NOK    TF Utility Path      Workspace             Item                            Version                 All    Overwrite    Force    Recursive    RETURN CODE    RETURN STATUS    MESSAGE
01_Success Get Changeset 85
                      OK        ${_TFS_UTIL_PATH}    ${GET_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    ${_TFS_BASE_VERSION}    No     Yes          No       Yes          0              ${ENDED_OK}      ${EMPTY}

02_Fail Get Wrong utility path
                      NOK       wrong                ${GET_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    ${EMPTY}                No     Yes          No       Yes          0              ${ENDED_NOK}     ${EMPTY}

03_Fail Get Wrong workspace path
                      NOK       ${_TFS_UTIL_PATH}    C:\\Projectswrong     ${_TFS_BASE_ITEM}\\MyProject    ${EMPTY}                No     Yes          No       Yes          0              ${ENDED_NOK}     ${EMPTY}

04_Fail Get Wrong item
                      NOK       ${_TFS_UTIL_PATH}    ${GET_working_dir}    wrong                           ${EMPTY}                No     Yes          No       Yes          0              ${ENDED_NOK}     ${EMPTY}

05_Fail Get version not exist
                      NOK       ${_TFS_UTIL_PATH}    ${GET_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    No                      No     Yes          No       Yes          0              ${ENDED_NOK}     ${EMPTY}

06_Success Get Label Version
                      OK        ${_TFS_UTIL_PATH}    ${GET_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    LAT_POINT               No     Yes          No       Yes          0              ${ENDED_OK}      ${EMPTY}

07_Success Get Date Version
                      OK        ${_TFS_UTIL_PATH}    ${GET_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    D2016-02-25             No     Yes          No       Yes          0              ${ENDED_OK}      ${EMPTY}

08_Success Get Blank Version
                      OK        ${_TFS_UTIL_PATH}    ${GET_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    ${EMPTY}                No     Yes          No       Yes          0              ${ENDED_OK}      ${EMPTY}

09_Success Get Latest Version
                      OK        ${_TFS_UTIL_PATH}    ${GET_working_dir}    ${_TFS_BASE_ITEM}\\MyProject    T                       No     Yes          No       Yes          0              ${ENDED_OK}      ${EMPTY}

*** Keywords ***
Get Template
    [Arguments]    ${OK_NOK}    ${TFS_UTIL_PATH}    ${TFS_WORKING_DIR}    ${TFS_ITEM}    ${TFS_VERSION}    ${TFS_ALL}
    ...    ${TFS_OVERWRITE}    ${TFS_FORCE}    ${TFS_RECURSIVE}    ${returnCode}    ${returnStatus}    ${message}
    [Documentation]    This template is used for test GET with needed arguments
    Init Action    ${ACTION_GET}
    Action Set    ${PRT_GET_TFS_UTIL_PATH}    ${TFS_UTIL_PATH}
    Action Set    ${PRT_GET_TFS_WORKING_DIR}    ${TFS_WORKING_DIR}
    Action Set    ${PRT_GET_TFS_ITEM}    ${TFS_ITEM}
    Action Set    ${PRT_GET_TFS_VERSION}    ${TFS_VERSION}
    Action Set    ${PRT_GET_TFS_ALL}    ${TFS_ALL}
    Action Set    ${PRT_GET_TFS_OVERWRITE}    ${TFS_OVERWRITE}
    Action Set    ${PRT_GET_TFS_FORCE}    ${TFS_FORCE}
    Action Set    ${PRT_GET_TFS_RECURSIVE}    ${TFS_RECURSIVE}
    Action Set Timeout    300
    Action Execute
    Action Report Should Be Found
    Run Keyword If    '${OK_NOK}'=='OK'    Action Return Code Should Be    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${message}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Start testing for Get Template
    Log    Delete Working Dir from previous test
    Delete Dir    ${GET_working_dir}
    Log    Delete Workspace from last test
    Delete Workspace    ${GET_workspace}
    Log    Finish remove data from previous test. Preparing test data...
    Create Dir    ${GET_working_dir}
    Create Workspace    ${GET_working_dir}    ${GET_workspace}

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing for Get Template end
