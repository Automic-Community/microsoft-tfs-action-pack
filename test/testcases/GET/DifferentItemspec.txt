*** Settings ***
Documentation     Run with different itemspec
Suite Setup       Suite Setup
Force Tags        GET    win
Test Template     Get Template
Resource          ../../resources/keywords.txt

*** Test Cases ***    ITEMSPEC                                  RETURN CODE    RETURN STATUS
Get One File          AutoTest\\Models\\AccountViewModels.cs    0              ${ENDED_OK}

Get All Star          AutoTest\\Models\\*                       0              ${ENDED_OK}

Get All Star Cs       AutoTest\\Models\\*.cs                    0              ${ENDED_OK}

*** Keywords ***
Get Template
    [Arguments]    ${itemspec}    ${returnCode}    ${returnStatus}
    Clean Up Directory    ${_WORKING_DIR}${/}Get
    Init Action    ${ACTION_GET}
    Action Set Timeout    1000
    Action Set    ${PRT_GET_TFS_UTIL_PATH}    ${_TF_EXE}
    Action Set    ${PRT_GET_TFS_WORKING_DIR}    ${_WORKING_DIR}${/}Get
    Action Set    ${PRT_GET_TFS_ITEM}    ${itemspec}
    Action Set    ${PRT_GET_TFS_VERSION}    T
    Action Set    ${PRT_GET_TFS_ALL}    NO
    Action Set    ${PRT_GET_TFS_OVERWRITE}    YES
    Action Set    ${PRT_GET_TFS_RECURSIVE}    YES
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
